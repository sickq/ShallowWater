#pragma kernel CSMain

//发生在相机位置移动
#pragma kernel UpdateBufferCS

RWStructuredBuffer<float> PrevPrevBuffer;
RWStructuredBuffer<float> PrevBuffer;
RWStructuredBuffer<float> CurrentBuffer;
RWTexture2D<float> _ShallowWaterHeightRT;

Texture2D _ShallowHeightMap;

float4 _ShallowWaterParams1;

int _ShallowWaterSize;
float TravelSpeed;
float Damping;

float GetPrevBufferValue(int indexX, int indexY, float fallbackValue)
{
    if(indexX < 0 || indexX > _ShallowWaterSize - 1 || indexY < 0 || indexY > _ShallowWaterSize - 1)
    {
        return fallbackValue;
    }
    int index = indexX + indexY * _ShallowWaterSize;
    return PrevBuffer[index];
}

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    if(id.x >= _ShallowWaterSize || id.y >= _ShallowWaterSize)
    {
        return;
    }
    int index = id.x + id.y * _ShallowWaterSize;

    float height = _ShallowHeightMap[id.xy] * -1 * _ShallowWaterParams1.x;
    
    float prevHeight = PrevBuffer[index];
    float nearHeightTotal = GetPrevBufferValue(id.x, id.y + 1, prevHeight) +
                            GetPrevBufferValue(id.x, id.y - 1, prevHeight) +
                            GetPrevBufferValue(id.x + 1, id.y, prevHeight) +
                            GetPrevBufferValue(id.x - 1, id.y, prevHeight);

    float prevprevHeight = PrevPrevBuffer[index];
    float outHeight = Damping * (prevHeight + (prevHeight - prevprevHeight)) + TravelSpeed * (nearHeightTotal - prevHeight * 4);
    if(height < 0)
    {
        outHeight = min(height, outHeight);
    }
    outHeight = max(outHeight, _ShallowWaterParams1.y);
    CurrentBuffer[index] = outHeight;
    _ShallowWaterHeightRT[id.xy] = outHeight;
}

RWStructuredBuffer<float> NewPrevPrevBuffer;
RWStructuredBuffer<float> NewPrevBuffer;
RWStructuredBuffer<float> NewCurrentBuffer;

float4 _ShallowBufferUpdateParams;

[numthreads(8,8,1)]
void UpdateBufferCS (uint3 id : SV_DispatchThreadID)
{
    if(id.x >= _ShallowWaterSize || id.y >= _ShallowWaterSize)
    {
        return;
    }
    int index = id.x + id.y * _ShallowWaterSize;

    int beforeXIndex = id.x + floor(_ShallowBufferUpdateParams.x * _ShallowWaterSize);
    int beforeYIndex = id.y - floor(_ShallowBufferUpdateParams.y * _ShallowWaterSize);

    if(beforeXIndex < 0 || beforeXIndex >= _ShallowWaterSize ||
       beforeYIndex < 0 || beforeYIndex >= _ShallowWaterSize)
    {
        NewPrevBuffer[index] = 0;
        NewPrevPrevBuffer[index] = 0;
        NewCurrentBuffer[index] = 0;
        return;
    }

    int beforeIndex = beforeXIndex + beforeYIndex * _ShallowWaterSize;
    
    NewPrevBuffer[index] = PrevBuffer[beforeIndex];
    NewPrevPrevBuffer[index] = PrevPrevBuffer[beforeIndex];
    NewCurrentBuffer[index] = CurrentBuffer[beforeIndex];
}